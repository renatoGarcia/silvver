#!/bin/env python

import sys
sys.path.insert(0, '../common')
from sconsUtilities import *

from os.path import abspath

mandatory_libs = LibraryDict()
mandatory_libs['boost'] = Library(name='Boost',
                                  headers=['boost/archive/text_iarchive.hpp',
                                           'boost/archive/text_oarchive.hpp',
                                           'boost/asio/buffer.hpp',
                                           'boost/asio/ip/tcp.hpp',
                                           'boost/asio/ip/udp.hpp',
                                           'boost/asio/placeholders.hpp',
                                           'boost/asio/read.hpp',
                                           'boost/asio/write.hpp',
                                           'boost/bind.hpp',
                                           'boost/lexical_cast.hpp',
                                           'boost/ref.hpp',
                                           'boost/scoped_ptr.hpp',
                                           'boost/thread/mutex.hpp',
                                           'boost/thread/once.hpp',
                                           'boost/thread/thread.hpp',
                                           'boost/tuple/tuple.hpp'],
                                  libs=['boost_serialization',
                                        'boost_system',
                                        'boost_thread'])

#------------------------------------------------------------------

AddOption('--prefix',
          default = '/usr/local',
          dest    = 'prefix',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix')
AddOption('--exec_prefix',
          default = GetOption('prefix'),
          dest    = 'exec_prefix',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix for executables and object '
                    'code libraries')
AddOption('--datadir',
          default = GetOption('prefix') + '/share',
          dest    = 'datadir',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix for machine-independent data')
AddOption('--docdir',
          default = GetOption('datadir') + '/doc',
          dest    = 'docdir',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix for documentation')
AddOption('--libdir',
          default = GetOption('exec_prefix') + '/lib',
          dest    = 'libdir',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix for object code libraries')
AddOption('--includedir',
          default = GetOption('prefix') + '/include',
          dest    = 'includedir',
          nargs   = 1,
          type    = 'string',
          action  = 'store',
          metavar = 'DIR',
          help    = 'Installation prefix for C header files')

mandatory_libs.add_options(AddOption)

var = Variables()

env = Environment(options = var)
for name,value in var.UnknownVariables().iteritems():
    env[name] = value

try:
    env['CPPPATH'] = env['CPPPATH'].split(';')
except:
    pass
env.Append(CPPPATH = [abspath('../common')])
try:
    env['LIBPATH'] = env['LIBPATH'].split(';')
except:
    pass

if not GetOption('clean') and not GetOption('help'):
    conf = Configure(env)

    print '\n' + 'Mandatory libraries...'
    if not mandatory_libs.check_libs(conf):
        exit(1)
    print

    env = conf.Finish()

install_nodes = []

install_nodes.append(env.Install(GetOption('includedir') + '/silvver',
                                 ['src/target.hpp',
                                  '../common/silvverTypes.hpp',
                                  '../common/serializations.hpp']))

install_nodes.append(env.Install(GetOption('docdir') + '/silvver_client',
                                 ['doc/silvver_client.cpp',
                                  'doc/Makefile']))

Export('env', 'install_nodes')
SConscript('./src/SConscript', build_dir='./build', duplicate=0)

env.Alias('install', install_nodes)
