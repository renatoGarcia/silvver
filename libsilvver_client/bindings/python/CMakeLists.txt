FIND_PACKAGE(SWIG)
FIND_PACKAGE(PythonLibs)
FIND_PACKAGE(PythonInterp)

IF(SWIG_FOUND AND PYTHONLIBS_FOUND AND PYTHONINTERP_FOUND)

  IF(PYTHON_OS_WIN)
    GET_FILENAME_COMPONENT(playercpyInstallDir ${PYTHON_EXECUTABLE} PATH)
  ELSE(PYTHON_OS_WIN)
    # Get the Python version
    EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE} -V
                    ERROR_VARIABLE pythonVersionString
                    ERROR_STRIP_TRAILING_WHITESPACE)
    STRING(REGEX REPLACE "^Python ([0-9]+\\.[0-9]+).*" "\\1"
           pythonVersion ${pythonVersionString})
  ENDIF(PYTHON_OS_WIN)

  SET(PYTHON_BINDING_INSTALL_DIR lib/python${pythonVersion}/site-packages
      CACHE PATH "Python binding installation directory under $prefix")
  MARK_AS_ADVANCED (PYTHON_BINDING_INSTALL_DIR)

  INCLUDE(${SWIG_USE_FILE})
  INCLUDE_DIRECTORIES(${PYTHON_INCLUDE_DIRS})

  SET(SOURCE pythonSilvver.i)
  SET_SOURCE_FILES_PROPERTIES(${SOURCE} PROPERTIES CPLUSPLUS ON)

  SWIG_ADD_MODULE(silvver python ${SOURCE})

  SWIG_LINK_LIBRARIES(silvver silvver_client)

  ADD_DEPENDENCIES(${SWIG_MODULE_silvver_REAL_NAME} silvver_client)

  SET(BINDINGS ${BINDINGS} "Python" PARENT_SCOPE)

  INSTALL(TARGETS ${SWIG_MODULE_silvver_REAL_NAME}
          LIBRARY DESTINATION ${PYTHON_BINDING_INSTALL_DIR})
  INSTALL(FILES ${CMAKE_BINARY_DIR}/bindings/python/silvver.py
          DESTINATION ${PYTHON_BINDING_INSTALL_DIR})

ENDIF(SWIG_FOUND AND PYTHONLIBS_FOUND AND PYTHONINTERP_FOUND)
